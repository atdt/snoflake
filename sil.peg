// PEG.js Grammar for SIL (SNOBOL Implementation Language)
// (C) Copyright 2012 by Ori Livneh
// Released under GPL and MIT licenses

{
    function Node(type, value) {
        this.type = type;
        this.value = value;
    }
}

program
  = lines:(statement / comment)+ end
{
    return lines;
}


comment
  = '*' comment:[^\n]* '\n'
{
    return comment.join('');
}

statement
  = label:label? blank oper:oper blank operands:operands blank? comment:[^\n]* '\n'
{
    return {
        operation : oper,
        operands  : operands,
        label     : label.value
    };
}

oper
  = oper:[A-Z]+
{
    return oper.join('');
}

operands
  = head:variable tail:(',' variable)*
{
    var result = [head];
    for (var i = 0, max = tail.length; i < max; i++) {
        result.push(tail[i][1]);
    }
    return result;
}

variable
  = list
  / literal
  / expr
  / label
  / integer
  / &(',') { return undefined; }


expr
  = left:(integer / label) operator:[*+-] right:(integer / label)
{
    return new Node('expression', [left, operator, right]);
}

literal "literal"
  = "'" literal:[^']* "'"
{
    return literal.join('');
}

integer "integer"
  = digits:[0-9]+
{
    return parseInt(digits.join(''), 10);
}

list "operand list"
  = '(' head:variable tail:(',' variable)* ')'
{
    var result = [head];
    for (var i = 0, max = tail.length; i < max; i++) {
        result.push(tail[i][1]);
    }
    return result;
}

blank
  = [\u0009\u0020]+


label "label"
  = head:[A-Z] tail:[A-Z0-9]*
{
    tail.unshift(head);
    return new Node('label', tail.join(''));
}

end
  = blank 'END' (blank / '\n')* !.

/* vim: set ft=javascript: */
