// PEG.js Grammar for SIL (SNOBOL Implementation Language)
// (C) Copyright 2012 by Ori Livneh
// Released under GPL and MIT licenses

{
    function Node(type, value) {
        this.type = type;
        this.value = value;
    }
}

program
  = lines:(statement / comment)+ end
{
    return lines;
}


comment
  = '*' comment:[^\n]* '\n'
{
    return comment.join('');
}

statement
  = loc:label? blank oper:oper blank operands:operands blank? comment:[^\n]* '\n'
{
    return [loc || null, oper, operands];
}

oper
  = oper:[A-Z]+
{
    return new Node('oper', oper.join(''));
}

operands
  = head:variable tail:(',' variable)*
{
    var result = [head];
    for (var i = 0, max = tail.length; i < max; i++) {
        result.push(tail[i][1]);
    }
    return new Node('operands', result);
}

variable
  = list
  / literal
  / label
  / integer
  / &(',') { return undefined; }

literal "literal"
  = "'" literal:[^']* "'"
{
    return new Node('literal', literal.join(''));
}

integer "integer"
  = digits:[0-9]+
{
    digits = parseInt(digits.join(''), 10);
    return new Node('integer', digits);
}

list "operand list"
  = '(' head:variable tail:(',' variable)* ')'
{
    var result = [head];
    for (var i = 0, max = tail.length; i < max; i++) {
        result.push(tail[i][1]);
    }
    return result;
}

item
  = value:(literal / label)
{
    return value;
}

blank
  = [\u0009\u0020]+


label "label"
  = head:[A-Z] tail:[A-Z0-9]*
{
    tail.unshift(head);
    return new Node('label', tail.join(''));
}

end
  = blank 'END' (blank / '\n')* !.
